@@grammar :: EHEX
#include :: "asp-core.ebnf"

(*   Epistemic HEX Input Language    *)
(*   Based on ASP-Core-2             *)


@override
strong_negated_atom::StrongNegation
    =
    op:(MINUS | '¬') atom:atom
    ;


@override
builtin_atom
    =
    | arithmetic_atom
    | binary_relation
    ;


arithmetic_atom::Atom
    =
    symbol:(
        | PLUS
        | MINUS
        | TIMES
        | DIV
        | '#int'
    )
    [PAREN_OPEN [arguments:terms] PAREN_CLOSE]
    ;


binary_relation::BinaryRelation
    =
    left:term op:relational_op right:term
    ;


@override
extended_literal
    =
    | default_negated_literal
    | modal_literal
    | classical_literal
    | builtin_atom
    ;


@override
default_negated_literal::DefaultNegation
    =
    op:NOT literal:(modal_literal | classical_literal | builtin_atom)
    ;

modal_literal
    =
    | k_modal
    | m_modal
    ;

k_modal::KModal::Modal
    =
    op:'K' literal:classical_literal
    ;

m_modal::MModal::Modal
    =
    op:'M' literal:classical_literal
    ;

# TODO?:
# epistemic_negation::EpistemicNegation
#     =
#     op:'!' literal:classical_literal
#     ;


@override ID = !'aux__' /[a-z][a-zA-Z0-9_]*/ ;
@override VARIABLE = !'AUX__' /[A-Z][a-zA-Z0-9_]*/ ;
@override OR = '|' | 'v';
@override CONS = ':-' | '←';
